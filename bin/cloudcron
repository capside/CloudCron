#!/usr/bin/env perl
use strict;
use warnings;
use v5.10;

use Cfn;
use CloudCron::Compiler;
use CloudCron::TargetQueue;
use JSON;
use Getopt::Long;

package CloudCron::Args {
    use Moose;
    with 'MooseX::Getopt';

    has file => (
        is => 'ro',
        isa => 'Str',
        required => 1,
        documentation => 'The crontab file to parse and convert to infrastructure.');

    has arn => (
        is => 'ro',
        isa => 'Str',
        required => 1,
        documentation => 'The Amazon Resource Name (ARN) of the target queue.');

    has id => (
        is => 'ro',
        isa => 'Str',
        required => 1,
        documentation => 'The user-defined unique id of the target queue.');
    1;
}

my $opts = CloudCron::Args->new_with_options;

my $file = $opts->file;
my $target = CloudCron::TargetQueue->new({
    Arn => $opts->arn,
    Id  => $opts->id,
});

my $cfn = Cfn->new;
my $compiler = CloudCron::Compiler->new({ file => $file, target => $target });
for my $linerule ($compiler->rules) {
    my $name = "cron-line-" . $linerule->line;
    $cfn->addResource($name, $linerule->rule);
}
my $hr = $cfn->as_hashref;

print encode_json($hr);
